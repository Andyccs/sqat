group 'com.sqatntu.stylechecker'
version '0.1.1'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'antlr'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'com.google.protobuf'
apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.7
mainClassName = "com.sqatntu.Main"

repositories {
    maven  {
        url "http://repo1.maven.org/maven2"
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.5" // use ANTLR version 4
    testCompile 'junit:junit:4.11'
    compile 'commons-io:commons-io:2.4'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-1'
    compile 'io.grpc:grpc-all:0.8.0'
}

buildscript {
    repositories {
        maven  {
            url "http://repo1.maven.org/maven2"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.0'
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0' // cobertura plugin
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:0.8.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

idea {
    module {
        sourceDirs += file('build/generated/source/proto/main/')
        sourceDirs += file('build/generated-src/antlr/main')
        excludeDirs -= file('build')
        excludeDirs += file('build/dependency-cache')
        excludeDirs += file('build/distributions')
        excludeDirs += file('build/extracted-include-protos')
        excludeDirs += file('build/libs')
        excludeDirs += file('build/reports')
        excludeDirs += file('build/resources')
        excludeDirs += file('build/scripts')
        excludeDirs += file('build/test-results')
        excludeDirs += file('build/tmp')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report
